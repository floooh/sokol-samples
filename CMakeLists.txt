#
# project: sokol-samples
#
cmake_minimum_required(VERSION 3.21)
set(CMAKE_C_STANDARD 11)
set(CMAKE_CXX_STANDARD 20)
project(sokol-samples)

if (SOKOL_USE_WGPU_DAWN)
    set(USE_DAWN_SDK ON)
endif()

# include the fips main cmake file
get_filename_component(FIPS_ROOT_DIR "../fips" ABSOLUTE)
include("${FIPS_ROOT_DIR}/cmake/fips.cmake")

fips_setup()
if (FIPS_IOS)
    set(CMAKE_OSX_DEPLOYMENT_TARGET "15.0" CACHE STRING "")
else()
    set(CMAKE_OSX_DEPLOYMENT_TARGET "14.0" CACHE STRING "")
endif()
fips_ide_group("Imports")
if (NOT USE_SOKOL_APP AND NOT SOKOL_USE_WGPU_DAWN AND NOT SOKOL_USE_D3D11 AND NOT SOKOL_USE_METAL)
    fips_import_fips_glfw_glfw3()
endif()
fips_import_fips_ozz_animation_ozzanimation()
fips_import_dcimgui_dcimgui()
fips_import_fips_libmodplug_libmodplug()
fips_ide_group("")

# just suppress this pesky "missing field initializer warning" for now
if (FIPS_CLANG OR FIPS_GCC)
    set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -Wno-missing-field-initializers -Wsign-conversion")
endif()

#  platform selection
add_definitions(-DSOKOL_NO_DEPRECATED)
set(HAS_COMPUTE_SHADERS ON)
if (FIPS_EMSCRIPTEN)
    if (FIPS_EMSCRIPTEN_USE_WEBGPU)
        set(sokol_backend SOKOL_WGPU)
        set(slang "wgsl")
    else()
        set(sokol_backend SOKOL_GLES3)
        set(slang "glsl300es")
        set(HAS_COMPUTE_SHADERS OFF)
    endif()
elseif (FIPS_ANDROID)
    set(sokol_backend SOKOL_GLES3)
    set(slang "glsl310es")
elseif (SOKOL_USE_WGPU_DAWN)
    set(sokol_backend SOKOL_WGPU)
    set(slang "wgsl")
elseif (SOKOL_USE_VULKAN)
    set(sokol_backend SOKOL_VULKAN)
    set(slang "spirv_vk")
elseif (SOKOL_USE_D3D11)
    set(sokol_backend SOKOL_D3D11)
    set(slang "hlsl5")
elseif (SOKOL_USE_METAL)
    set(sokol_backend SOKOL_METAL)
    if (FIPS_IOS)
        set(slang "metal_ios:metal_sim")
    else()
        set(slang "metal_macos")
    endif()
elseif (SOKOL_USE_EGL_GLES3)
    set(sokol_backend SOKOL_GLES3)
    set(slang "glsl310es")
elseif (SOKOL_USE_EGL_GL)
    set(sokol_backend SOKOL_GLCORE)
    set(slang "glsl430")
else()
    if (FIPS_IOS)
        set(sokol_backend SOKOL_GLES3)
        set(slang "glsl300es")
        set(HAS_COMPUTE_SHADERS OFF)
    else()
        set(sokol_backend SOKOL_GLCORE)
        if (FIPS_MACOS)
            set(slang "glsl410")
            set(HAS_COMPUTE_SHADERS OFF)
        else()
            set(slang "glsl430")
        endif()
    endif()
endif()

if (USE_SOKOL_APP)
    include_directories(libs)
    fips_ide_group("Libs")
    add_subdirectory(libs)
    fips_ide_group("Samples")
    add_subdirectory(sapp)
else()
    if (FIPS_EMSCRIPTEN)
        if (FIPS_EMSCRIPTEN_USE_WEBGPU)
            add_subdirectory(wgpu)
        else()
            add_subdirectory(html5)
        endif()
    elseif (SOKOL_USE_D3D11)
        add_subdirectory(d3d11)
    elseif (SOKOL_USE_METAL)
        add_subdirectory(metal)
    elseif (SOKOL_USE_WGPU_DAWN)
        add_subdirectory(wgpu)
    else()
        add_subdirectory(glfw)
    endif()
endif()
